/// The "Remote Settings" field in the Business Service settings should include the following:
/// 	<ul>
/// <li><u>DebugLogFile</u><br/>Path and filename for debug log file. To disable logging, leave this blank<br/><br/></li>
/// <li><u>GCPCredentials</u><br/>This is a JSON string generated by GCP. Note: the JSON string should be only one line -- remove all carriage returns/linefeeds before entering the string into Remove Settings<br/><br/></li>
/// <li><u>GCPProjectID</u><br/>The GCP project that the subscription belongs to<br/><br/></li>
/// <li><u>GCPSubscriptionID</u><br/>The GCP ID for the subscription to receive messages for<br/><br/></li>
/// <li><u>GCPTopicEncoding</u><br/>The encoding type for messages in this topic. This should be JSON (default) or BINARY. JSON data will be UTF-8 encoded<br/><br/></li>
/// <li><u>MessageClass</u><br/>The fully qualified name of the IRIS message class that should be sent to OnProcessInput. Unless a custom message type is needed, use PEX.GCP.PubSub.Msg.Message<br/><br/></li>
/// <li><u>InternalQueueSize</u><br/>The maximum size of the internal message queue. Defaults to 1000</li>
/// </ul>
/// See PEX documentation for more information about the Remote Settings field.
Class PEX.GCP.PubSub.Service Extends Ens.BusinessService
{

Parameter ADAPTER = "EnsLib.PEX.InboundAdapter";

Parameter SETTINGS = "TargetConfigNames:Basic:selector?multiSelect=1&context={Ens.ContextSearch/ProductionItems?targets=1&productionName=@productionId}";

Property TargetConfigNames As %String(MAXLEN = 1000);

Method OnProcessInput(pInput As %RegisteredObject, Output pOutput As %RegisteredObject) As %Status
{
	set tStatus = $$$OK
	set ..%WaitForNextCallInterval=1
	
	if $ISOBJECT(pInput) {
		for i=1:1:$length(..TargetConfigNames,",") {
			Set tStatus = ..SendRequestAsync($piece(..TargetConfigNames,",", i), pInput)
			If $$$ISERR(tStatus) Quit
		}
		set ..%WaitForNextCallInterval=0
	}

	quit tStatus
}

Method OnInit() As %Status
{
	set tStatus = ##super()
	
	throw:'$L(..TargetConfigNames) ##class(%Exception.StatusException).CreateFromStatus($$$ERROR(5001,"Must specify TargetConfigNames"))
	
	quit tStatus
}

}
